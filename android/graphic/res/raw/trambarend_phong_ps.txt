precision mediump float;

// Simple implementation of the phong illumination model.
vec4 phong(vec3 n, vec3 v, vec3 s, vec3 l, vec3 ka, vec3 kd, vec3 ks, float ke) {

  //if(dot(v,n) < 0.0) return vec4(0,0,0,0); // back-face

  // Normalization is good.
  n = normalize(n);
  v = normalize(v);
  s = normalize(s);

  // The ambient term.
  vec3 color = ka * l;

  float cosns = dot(n, s);
  if (cosns > 0.0) {
    // The diffuse term.
    color += kd * l * cosns;

    // The specular term.
    vec3 r = 2.0 * (cosns * n) - s;
    float cosrv = dot(r, v);
    if (cosrv > 0.0)
      color += ks * l * pow(cosrv, ke);
  }

  return vec4(color,1.0);
}

// Vertex attributes form the mesh. Transformed by the vertex shader.
varying vec3 fcolor;
varying vec3 normalV;
varying vec3 eyedirV;
varying vec3 lightdirV;
varying vec2 uvV;
uniform sampler2D colorMap;

uniform int hasLighting;

struct PhongMaterial
{
   vec4 emission;    // Ecm
   vec4 ambient;     // Acm
   vec4 diffuse;     // Dcm
   vec4 specular;    // Scm
   float shininess;  // Srm
};
uniform PhongMaterial material;

vec3 lightintensity = vec3(0.9, 0.9, 0.9);

void main() {

  vec4 texColor = texture2D(colorMap, uvV);

  gl_FragColor =  hasLighting == 2 ? texColor * phong(normalV, eyedirV, lightdirV, lightintensity, material.ambient.rgb, material.diffuse.rgb, material.specular.rgb, material.shininess) : texColor;
    //gl_FragColor =  phong(normalV, eyedirV, lightdirV, lightintensity, material.ambient.rgb, material.diffuse.rgb, material.specular.rgb, material.shininess);
  //gl_FragColor =  texture2D(colorMap, uvV);
}
